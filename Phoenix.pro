# Generated by Qt.
#
# Copyright (C) Sapura Secured Technologies, 2013-2025. All Rights Reserved.
#
# @file
# @version $Id: Phoenix.pro 1898 2025-01-17 00:50:10Z rosnin $

#prevent windows.h from including winsock.h
DEFINES += WIN32_LEAN_AND_MEAN
#prevent error with QDateTime
DEFINES += NOMINMAX

DEFINES += SETTINGS_INIFILE
DEFINES += SIPTCP
DEFINES += GIS_ROUTING
DEFINES += DGNA_OF_GRPS
DEFINES += INCIDENT
DEFINES += MSG_AES

#PostgreSQL database management system path
#comment out if not installed
DBPATH = $$quote(C:/Program Files/PostgreSQL/9.6)
#OpenSSL libraries
LIBS += -llibssl -llibcrypto

CONFIG(debug, debug|release) {
    DEFINES += DEBUG
    !defined(DBPATH, var) {
        DEFINES += NO_DB
    }
    LIBS += -lQXlsx_dbg
    !contains(DEFINES, NO_VOIP) {
        LIBS += -llibuclmm_dbg -lsrtp2_dbg
    }
}

CONFIG(release, debug|release) {
    DEFINES += NDEBUG
    LIBS += -lQXlsx
    !contains(DEFINES, NO_VOIP) {
        LIBS += -llibuclmm -lsrtp2
    }
}

defined(DBPATH, var) {
    INCLUDEPATH += $$quote($${DBPATH}/include)
    LIBS += $$quote($${DBPATH}/lib/libpq.lib)
}

#-------------------------------------------------
#
# Project created by QtCreator 2013-09-19T14:27:58
#
#-------------------------------------------------

QT += core gui widgets multimedia multimediawidgets printsupport qml quick \
      quickwidgets positioning location xml network

TARGET = scad
TEMPLATE = app

#modules arranged in 4 blocks: non-Qt, Qt, GIS-non-Qt and GIS-Qt
#in alphabetical order within each block
SOURCES += \
    CmnTypes.cpp \
    DbInt.cpp \
    Logger.cpp \
    MD5.c \
    Md5Digest.cpp \
    MmsClient.cpp \
    MsgSip.cpp \
    MsgSp.cpp \
    Props.cpp \
    ResourceData.cpp \
    RestApiClient.cpp \
    ServerSession.cpp \
    Socket.cpp \
    StatusCodes.cpp \
    SubsData.cpp \
    TcpSocket.cpp \
    UdpSocket.cpp \
    Utils.cpp \
    VideoStream.cpp \
    VoipSessionClient.cpp \
    VoipSessionBase.cpp \
    AudioDevice.cpp \
    AudioManager.cpp \
    AudioPlayer.cpp \
    Call.cpp \
    CallButton.cpp \
    CallWindow.cpp \
    CommsRegister.cpp \
    Contact.cpp \
    DateTimeDelegate.cpp \
    Dgna.cpp \
    Document.cpp \
    Draggable.cpp \
    DraggableListView.cpp \
    DraggableListWidget.cpp \
    FlowLayout.cpp \
    GpsMonitor.cpp \
    InputDialog.cpp \
    Login.cpp \
    Main.cpp \
    MainWindow.cpp \
    MessageDialog.cpp \
    Mms.cpp \
    Poi.cpp \
    QtTableUtils.cpp \
    QtUtils.cpp \
    Report.cpp \
    ResourceButton.cpp \
    Resources.cpp \
    ResourceSelector.cpp \
    RscCollector.cpp \
    Sds.cpp \
    Settings.cpp \
    SettingsUi.cpp \
    Style.cpp \
    Updater.cpp \
    Version.cpp \
    VideoDevice.cpp \
    GisLocation.cpp \
    GisBookmarks.cpp \
    GisCanvas.cpp \
    GisDgnaList.cpp \
    GisHistory.cpp \
    GisLayerList.cpp \
    GisLegend.cpp \
    GisMapOverview.cpp \
    GisMenuTerminal.cpp \
    GisMenuUserPoi.cpp \
    GisPoint.cpp \
    GisQmlInt.cpp \
    GisRouting.cpp \
    GisTracking.cpp \
    GisTrackingReplay.cpp \
    GisTrailingSelector.cpp \
    GisWindow.cpp

!contains(DEFINES, NO_VOIP) {
    SOURCES += RtpSession.cpp
}

HEADERS += \
    CmnTypes.h \
    DbInt.h \
    Locker.h \
    Logger.h \
    MD5.h \
    Md5Digest.h \
    MmsClient.h \
    MsgSip.h \
    MsgSp.h \
    MsgValueBase.h \
    PalFiles.h \
    PalLock.h \
    PalSem.h \
    PalSocket.h \
    PalThread.h \
    PalTime.h \
    Props.h \
    ResourceData.h \
    RestApiClient.h \
    RtpSession.h \
    ServerSession.h \
    Socket.h \
    StatusCodes.h \
    SubsData.h \
    TcpSocket.h \
    UdpSocket.h \
    Utils.h \
    VideoStream.h \
    VoipSessionClient.h \
    VoipSessionBase.h \
    AudioDevice.h \
    AudioManager.h \
    AudioPlayer.h \
    Call.h \
    CallButton.h \
    CallWindow.h \
    CommsRegister.h \
    Contact.h \
    DateTimeDelegate.h \
    Dgna.h \
    Document.h \
    Draggable.h \
    DraggableListView.h \
    DraggableListWidget.h \
    FlowLayout.h \
    GpsMonitor.h \
    InputDialog.h \
    Login.h \
    MainWindow.h \
    MessageDialog.h \
    Mms.h \
    Poi.h \
    QThreadWorker.h \
    QtTableUtils.h \
    QtUtils.h \
    Report.h \
    ResourceButton.h \
    Resources.h \
    ResourceSelector.h \
    RscCollector.h \
    Sds.h \
    Settings.h \
    SettingsUi.h \
    Style.h \
    Updater.h \
    Version.h \
    VideoDevice.h \
    GisLocation.h \
    GisBookmarks.h \
    GisCanvas.h \
    GisDgnaList.h \
    GisHistory.h \
    GisLayerList.h \
    GisLegend.h \
    GisMapOverview.h \
    GisMenuTerminal.h \
    GisMenuUserPoi.h \
    GisPoint.h \
    GisQmlInt.h \
    GisRouting.h \
    GisTracking.h \
    GisTrackingReplay.h \
    GisTrailingSelector.h \
    GisWindow.h \
    rapidxml.hpp \
    rapidxml_utils.hpp \
    config_win32.h \
    rtp.h

FORMS += \
    AudioPlayer.ui \
    Call.ui \
    CallButton.ui \
    CallWindow.ui \
    CommsRegister.ui \
    Contact.ui \
    Dgna.ui \
    GpsMonitor.ui \
    Login.ui \
    MainWindow.ui \
    MessageDialog.ui \
    Report.ui \
    Resources.ui \
    ResourceSelector.ui \
    RscCollector.ui \
    Sds.ui \
    SettingsUi.ui \
    Updater.ui \
    GisBookmarks.ui \
    GisDgnaList.ui \
    GisLayerList.ui \
    GisLegend.ui \
    GisMapOverview.ui \
    GisMenuTerminal.ui \
    GisMenuUserPoi.ui \
    GisRouting.ui \
    GisTracking.ui \
    GisTrackingReplay.ui \
    GisTrailingSelector.ui \
    GisWindow.ui

contains(DEFINES, MSG_AES) {
    SOURCES += Aes.cpp
    HEADERS += Aes.h
}

contains(DEFINES, INCIDENT) {
    SOURCES += ActiveIncidents.cpp \
               Incident.cpp \
               IncidentButton.cpp \
               IncidentData.cpp

    HEADERS += ActiveIncidents.h \
               Incident.h \
               IncidentButton.h \
               IncidentData.h

    FORMS += ActiveIncidents.ui \
             Incident.ui \
             IncidentButton.ui
}

!contains(DEFINES, NO_VIDEO) {
    DEFINES += FFMPEG
    SOURCES += VideoDecoder.cpp \
               VideoEncoder.cpp
    HEADERS += VideoDecoder.h \
               VideoEncoder.h
    INCLUDEPATH += ffmpeg
    LIBS += -lavcodec -lavutil -lswscale -llibx264
}

RESOURCES += \
    Resources.qrc

TRANSLATIONS = translations/Scad_ms.ts

message(INCLUDEPATH = $$INCLUDEPATH)
message(LIBS = $$LIBS)
